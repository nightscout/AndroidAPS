// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        kotlin_version = '1.6.10'
        core_version = '1.7.0'
        rxjava_version = '3.1.3'
        rxandroid_version = '3.0.0'
        rxkotlin_version = '3.0.1'
        room_version = '2.4.2'
        lifecycle_version = '2.3.1'
        dagger_version = '2.41'
        coroutines_version = '1.4.1'
        activity_version = '1.3.1'
        fragmentktx_version = '1.3.6'
        ormLite_version = '4.46'
        nav_version = '2.3.5'
        appcompat_version = '1.4.1'
        material_version = '1.4.0'
        constraintlayout_version = '2.1.0'
        preferencektx_version = '1.2.0'
        commonslang3_version = '3.11'
        commonscodec_version = '1.15'
        jodatime_version = '2.10.13'
        work_version = '2.5.0'
        tink_version = '1.5.0'

        junit_version = '4.13.2'
        mockito_version = '4.4.0'
        dexmaker_version = "1.2"
        retrofit2_version = '2.9.0'
        okhttp3_version = '4.9.0'
        byteBuddy_version = '1.12.8'

        androidx_junit_version = '1.1.2'
        androidx_rules_version = '1.4.0'
    }
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" } // jacoco 0.2
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.2'
        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.8.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath 'com.hiya:jacoco-android:0.2'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.19.0"
    id "org.jlleitschuh.gradle.ktlint" version "10.2.1"
    id 'org.barfuin.gradle.jacocolog' version '2.0.0'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
        maven { url 'https://jitpack.io' }
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs = [
                    // Ignore warning for  @ExperimentalCoroutinesApi
                    "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                    '-Xjvm-default=all'     //Support @JvmDefault
            ]
            jvmTarget = "11"
        }
    }
    // Workaround to support M1 builds. Room 2.4.0 fixes this but requires min compileSdkVersion
    // 31 => remove when upgrading to Room 2.4.0
    // Source: https://issuetracker.google.com/issues/174695268
    configurations.configureEach {
        resolutionStrategy {
            force 'org.xerial:sqlite-jdbc:3.36.0.3'
        }
    }
}

apply from: 'gradle/jacoco_project.gradle'

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    tasks.withType(Test) {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }
}
