buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def generateGitBuild = { ->

    StringBuilder stringBuilder = new StringBuilder();
    stringBuilder.append('"')
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always'
            standardOutput = stdout
        }
        String commitObject = stdout.toString().trim()
        stringBuilder.append(commitObject)
    } catch (ignored) {
        stringBuilder.append('NoGitSystemAvailable')
    }
    stringBuilder.append('-')
    stringBuilder.append((new Date()).format('yyyy.MM.dd'))
    stringBuilder.append('"')
    return stringBuilder.toString()
}

android {
    compileSdkVersion 23
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "info.nightscout.androidaps"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1100
        versionName "1.1e"
        buildConfigField "String", "BUILDVERSION", generateGitBuild()
    }
    lintOptions {
        disable 'MissingTranslation'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        flavorDimensions "standard", "limits",  "wear"
        adult {
            dimension "limits"
            buildConfigField "int", "MAXBOLUS", "17"
        }
        teenage {
            dimension "limits"
            buildConfigField "int", "MAXBOLUS", "10"
        }
        child {
            dimension "limits"
            buildConfigField "int", "MAXBOLUS", "5"
        }
        full {
            dimension "standard"
            buildConfigField "boolean", "APS", "true"
            buildConfigField "boolean", "PUMPDRIVERS", "true"
            buildConfigField "boolean", "CLOSEDLOOP", "true"
        }
        pumpcontrol {
            dimension "standard"
            buildConfigField "boolean", "APS", "false"
            buildConfigField "boolean", "PUMPDRIVERS", "true"
            buildConfigField "boolean", "CLOSEDLOOP", "false"
        }
        careportal {
            dimension "standard"
            buildConfigField "boolean", "APS", "false"
            buildConfigField "boolean", "PUMPDRIVERS", "false"
            buildConfigField "boolean", "CLOSEDLOOP", "false"
        }
        openloop {
            dimension "standard"
            buildConfigField "boolean", "APS", "true"
            buildConfigField "boolean", "PUMPDRIVERS", "true"
            buildConfigField "boolean", "CLOSEDLOOP", "false"
        }
        wear {
            dimension "wear"
            buildConfigField "boolean", "WEAR", "true"
        }
        nowear {
            dimension "wear"
            buildConfigField "boolean", "WEAR", "false"
        }
    }
}

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
}

dependencies {
    wearWearApp project(path: ':wear', configuration: 'fullRelease')
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile('com.crashlytics.sdk.android:crashlytics:2.5.7@aar') {
        transitive = true;
    }

    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:gridlayout-v7:23.4.0'
    compile "com.android.support:design:23.4.0"
    compile "com.android.support:percent:23.4.0"
    compile 'com.wdullaer:materialdatetimepicker:2.3.0'
    compile 'com.squareup:otto:1.3.7'
    compile 'com.j256.ormlite:ormlite-core:4.46'
    compile 'com.j256.ormlite:ormlite-android:4.46'
    compile('com.github.tony19:logback-android-classic:1.1.1-6') {
        exclude group: 'com.google.android', module: 'android'
    }
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'com.jjoe64:graphview:4.0.1'
    compile 'com.eclipsesource.j2v8:j2v8:3.1.6@aar'
    compile 'com.joanzapata.iconify:android-iconify-fontawesome:2.1.1'
    compile 'com.google.android.gms:play-services-wearable:7.5.0'
    compile 'junit:junit:4.12'
    testCompile 'org.json:json:20140107'
    testCompile 'org.mockito:mockito-core:2.+'
    androidTestCompile 'org.mockito:mockito-core:2.+'
    androidTestCompile "com.google.dexmaker:dexmaker:1.2"
    androidTestCompile "com.google.dexmaker:dexmaker-mockito:1.2"
    compile(name:'android-edittext-validator-v1.3.4-mod', ext:'aar')

}