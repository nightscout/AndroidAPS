buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def generateGitBuild = { ->

    StringBuilder stringBuilder = new StringBuilder();
    stringBuilder.append('"')
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always'
            standardOutput = stdout
        }
        String commitObject = stdout.toString().trim()
        stringBuilder.append(commitObject)
    } catch (ignored) {
        stringBuilder.append('NoGitSystemAvailable')
    }
    stringBuilder.append('-')
    stringBuilder.append((new Date()).format('yyyy.MM.dd'))
    stringBuilder.append('"')
    return stringBuilder.toString()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "info.nightscout.androidaps"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1020
        versionName "1.0.20"
        buildConfigField "String", "BUILDVERSION", generateGitBuild()
    }
    lintOptions {
        disable 'MissingTranslation'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        flavorDimensions "standard", "wear"
        full {
            dimension "standard"
            buildConfigField "boolean", "APS", "true"
            buildConfigField "boolean", "PUMPDRIVERS", "true"
            buildConfigField "boolean", "CLOSEDLOOP", "true"
            buildConfigField "int", "MAXBOLUS", "17"
        }
        fullteenage {
            dimension "standard"
            buildConfigField "boolean", "APS", "true"
            buildConfigField "boolean", "PUMPDRIVERS", "true"
            buildConfigField "boolean", "CLOSEDLOOP", "true"
            buildConfigField "int", "MAXBOLUS", "10"
        }
        fullchild {
            dimension "standard"
            buildConfigField "boolean", "APS", "true"
            buildConfigField "boolean", "PUMPDRIVERS", "true"
            buildConfigField "boolean", "CLOSEDLOOP", "true"
            buildConfigField "int", "MAXBOLUS", "5"
        }
        danarcontrol {
            dimension "standard"
            buildConfigField "boolean", "APS", "false"
            buildConfigField "boolean", "PUMPDRIVERS", "true"
            buildConfigField "boolean", "CLOSEDLOOP", "false"
            buildConfigField "int", "MAXBOLUS", "17"
        }
        careportal {
            dimension "standard"
            buildConfigField "boolean", "APS", "false"
            buildConfigField "boolean", "PUMPDRIVERS", "false"
            buildConfigField "boolean", "CLOSEDLOOP", "false"
            buildConfigField "int", "MAXBOLUS", "17"
        }
        openloop {
            dimension "standard"
            buildConfigField "boolean", "APS", "true"
            buildConfigField "boolean", "PUMPDRIVERS", "true"
            buildConfigField "boolean", "CLOSEDLOOP", "false"
            buildConfigField "int", "MAXBOLUS", "17"
        }
        wear {
            dimension "wear"
            buildConfigField "boolean", "WEAR", "true"
        }
        nowear {
            dimension "wear"
            buildConfigField "boolean", "WEAR", "false"
        }
    }
}

dependencies {
    wearWearApp project(path: ':wear', configuration: 'fullRelease')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile('com.crashlytics.sdk.android:crashlytics:2.5.7@aar') {
        transitive = true;
    }
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:gridlayout-v7:23.4.0'
    compile 'com.wdullaer:materialdatetimepicker:2.3.0'
    compile 'com.squareup:otto:1.3.7'
    compile 'com.j256.ormlite:ormlite-core:4.46'
    compile 'com.j256.ormlite:ormlite-android:4.46'
    compile('com.github.tony19:logback-android-classic:1.1.1-6') {
        exclude group: 'com.google.android', module: 'android'
    }
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'com.jjoe64:graphview:4.0.1'
    compile 'com.eclipsesource.j2v8:j2v8:3.1.6@aar'
    compile 'com.joanzapata.iconify:android-iconify-fontawesome:2.1.1'
    testCompile 'junit:junit:4.12'
    compile 'com.google.android.gms:play-services-wearable:7.5.0'
}
